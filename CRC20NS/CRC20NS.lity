pragma lity ^1.2.4;

contract CRC20 {
    function symbol() public view returns (string);
    function owner() public view returns (address);
}

contract CRC20NS {

    struct Entry {
        address contractAddr;
        uint createTime;
        bool initialized;
    }
    
    mapping(string => Entry) entries;
    mapping(string => bool) forbiddenSymbols;
    mapping(uint256 => safeuint) fees;

    address public owner;
    constructor () public {
        owner = msg.sender;
        forbiddenSymbols["CMT"] = true;
        forbiddenSymbols["BTC"] = true;
        forbiddenSymbols["ETH"] = true;
        forbiddenSymbols["SEX"] = true;
        forbiddenSymbols["FUCK"] = true;

        fees[0] = 1000000000000000000; // 1 CMT This is the default
        fees[1] = 1; // 1 wei to signal that this is not allowed
        fees[2] = 1000000000000000000000; // 1000 CMT
        fees[3] = 100000000000000000000;  // 100 CMT
        fees[4] = 10000000000000000000;   // 10 CMT
    }
    modifier onlyOwner() {
        assert(msg.sender == owner);
        _;
    }

    function register (address _contractAddr) public payable {
        CRC20 crc = CRC20(_contractAddr);
        string memory symbol = crc.symbol();
        require (forbiddenSymbols[symbol] != true);
        require (msg.sender == crc.owner());

        safeuint fee = fees[bytes(symbol).length];
        if (fee == 0) {
            fee = fees[0];
        }
        require (fee > 1); // exit if the fee is 1 wei
        require (msg.value >= fee);

        Entry memory entry = entries[symbol];
        if (entry.initialized) {
            require (now > entry.createTime + 31557600); // Must have expired
        }

        Entry memory newEntry = Entry(_contractAddr, now, true);
        entries[symbol] = newEntry;
    }

    function forceRemove (string _symbol) external onlyOwner {
         delete entries[_symbol];
    }

    function lookup (string _symbol) public view returns (address, uint) {
        Entry memory entry = entries[_symbol];
        return (entry.contractAddr, entry.createTime);
    }

    function isRegistered (string _symbol, address _contractAddr) public view returns (bool) {
        Entry memory entry = entries[_symbol];
        return (entry.contractAddr == _contractAddr);
    }

    function updateFee (uint _symbol_length, safeuint fee) external onlyOwner {
        fees[_symbol_length] = fee;
    }

    function findFee (uint _symbol_length) public view returns (safeuint) {
        return fees[_symbol_length];
    }

    function addForbiddenSymbol (string _symbol) external onlyOwner {
        require (forbiddenSymbols[_symbol] != true);
        forbiddenSymbols[_symbol] = true;
    }

    function deleteForbiddenSymbol (string _symbol) external onlyOwner {
        require (forbiddenSymbols[_symbol] == true);
        delete forbiddenSymbols[_symbol];
    }

    function isForbiddenSymbol (string _symbol) public view returns (bool) {
        return forbiddenSymbols[_symbol];
    }

    function getBalance () external onlyOwner view returns (safeuint) {
        return address(this).balance;
    }
    
    function withdraw (safeuint _amount) external onlyOwner {
        require (address(this).balance >= _amount);
        owner.transfer(uint256(_amount));
    }

    function terminate() external onlyOwner {
        selfdestruct(owner);
    }
}
